plugins {
  id 'java'
  id 'com.github.johnrengelman.shadow' version '1.2.3'
  id 'com.commercehub.gradle.plugin.avro' version "0.5.0"
}

archivesBaseName = 'AdImpressionLogAggr'

allprojects {

  version = '0.5'

  repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/groups/public" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url "http://repository.snappydata.io:8089/repository/internal" }
    maven { url "http://repository.snappydata.io:8089/repository/snapshots" }
    maven { url "http://mvnrepository.com/artifact" }
    maven { url 'https://clojars.org/repo' }
  }

  apply plugin: 'java'
  apply plugin: 'maven'
  apply plugin: 'scala'
  apply plugin: 'idea'
  apply plugin: 'eclipse'
  apply plugin: "com.commercehub.gradle.plugin.avro"

  configurations {
    provided {
      description 'a dependency that is provided externally at runtime'
      visible true
    }
  }

  idea {
    module {
      scopes.PROVIDED.plus += [ configurations.provided ]
    }
  }
  eclipse {
    classpath {
      plusConfigurations += [ configurations.provided ]
    }
  }

  sourceSets {
    main.compileClasspath += configurations.provided
    main.runtimeClasspath -= configurations.provided
    test.compileClasspath += configurations.provided
    test.runtimeClasspath += configurations.provided
  }

  javadoc.classpath += configurations.provided

  ext {
    sparkVersion = '2.0.1-2'
    scalaVersion = '2.11'
    snappyVersion = '0.6'
  }

  configurations.all {
    resolutionStrategy.cacheChangingModulesFor 4, 'hours'
  }

  dependencies {
    compile 'org.scala-lang:scala-library:2.10.6'
    compile 'org.scala-lang:scala-reflect:2.10.6'
    compile 'org.scala-lang:scala-compiler:2.10.6'
  }
}

dependencies {
  provided "io.snappydata:snappydata-core_${scalaVersion}:${snappyVersion}"
  provided "io.snappydata:snappydata-cluster_${scalaVersion}:${snappyVersion}"
  provided "io.snappydata:snappy-spark-core_${scalaVersion}:${sparkVersion}"
  provided "io.snappydata:snappy-spark-catalyst_${scalaVersion}:${sparkVersion}"
  provided "io.snappydata:snappy-spark-sql_${scalaVersion}:${sparkVersion}"
  provided "io.snappydata:snappydata-aqp_${scalaVersion}:${snappyVersion}"
  provided group: 'io.snappydata', name: 'snappy-spark-streaming-kafka-0.8_2.11', version: '2.0.1-2'

  compile 'com.miguno:kafka-avro-codec_2.10:0.1.1-SNAPSHOT'
  compile 'org.apache.kafka:kafka_2.10:0.8.2.1'
  compile 'com.twitter:algebird-core_2.10:0.1.11'
  compile 'com.googlecode.javaewah:JavaEWAH:1.1.5'
  compile 'org.joda:joda-convert:1.2'
  compile 'com.opencsv:opencsv:3.3'
}

task generateAvro(type: com.commercehub.gradle.plugin.avro.GenerateAvroJavaTask) {
  source("src/avro")
  outputDir = file("src/main/java")
}

compileJava.source(generateAvro.outputs)

avro.stringType = "charSequence"


ext {
  assemblyJar = rootProject.tasks.getByPath(':assembly:shadowJar').outputs
}

def assemblyJar = tasks.getByPath(':assembly:shadowJar').outputs

task generateAdImpressions(type: JavaExec, dependsOn: classes) {
  main = 'io.snappydata.adanalytics.KafkaAdImpressionProducer'
  classpath sourceSets.test.runtimeClasspath
  environment 'PROJECT_ASSEMBLY_JAR', assemblyJar.files.asPath
}

task aggeregateAdImpressions_API(type: JavaExec, dependsOn: classes) {
  main = 'io.snappydata.adanalytics.SnappyAPILogAggregator'
  jvmArgs = ['-XX:MaxPermSize=512m']
  classpath sourceSets.test.runtimeClasspath
  environment 'PROJECT_ASSEMBLY_JAR', assemblyJar.files.asPath
}

task aggeregateAdImpressions_SQL(type: JavaExec, dependsOn: classes) {
  main = 'io.snappydata.adanalytics.SnappySQLLogAggregator'
  jvmArgs = ['-XX:MaxPermSize=512m']
  classpath sourceSets.test.runtimeClasspath
  environment 'PROJECT_ASSEMBLY_JAR', assemblyJar.files.asPath
}

task generateAdImpressions_Socket(type: JavaExec, dependsOn: classes) {
  main = 'io.snappydata.benchmark.SocketAdImpressionGenerator'
  classpath sourceSets.test.runtimeClasspath
  environment 'PROJECT_ASSEMBLY_JAR', assemblyJar.files.asPath
  maxHeapSize = "8196m"
}

task startSnappyIngestionPerf_Socket(type: JavaExec, dependsOn: classes) {
  main = 'io.snappydata.benchmark.SocketSnappyIngestionPerf'
  jvmArgs = ['-XX:MaxPermSize=512m']
  maxHeapSize = "8196m"
  classpath sourceSets.test.runtimeClasspath
  environment 'PROJECT_ASSEMBLY_JAR', assemblyJar.files.asPath
}

task startSnappyIngestionPerf_CustomReceiver(type: JavaExec, dependsOn: classes) {
  main = 'io.snappydata.benchmark.CustomReceiverSnappyIngestionPerf'
  jvmArgs = ['-XX:MaxPermSize=512m']
  maxHeapSize = "8196m"
  classpath sourceSets.test.runtimeClasspath
  environment 'PROJECT_ASSEMBLY_JAR', assemblyJar.files.asPath
}

task startSnappyIngestionPerf_CSV(type: JavaExec, dependsOn: classes) {
  main = 'io.snappydata.benchmark.CSVSnappyIngestionPerf'
  jvmArgs = ['-XX:MaxPermSize=512m']
  maxHeapSize = "8196m"
  classpath sourceSets.test.runtimeClasspath
  environment 'PROJECT_ASSEMBLY_JAR', assemblyJar.files.asPath
}

task startSnappyIngestionPerf_Kafka(type: JavaExec, dependsOn: classes) {
  main = 'io.snappydata.benchmark.KafkaSnappyIngestionPerf'
  jvmArgs = ['-XX:MaxPermSize=512m']
  maxHeapSize = "8196m"
  classpath sourceSets.test.runtimeClasspath
  environment 'PROJECT_ASSEMBLY_JAR', assemblyJar.files.asPath
}

task product(type: Exec) {
  dependsOn ':assembly:shadowJar'

  def productDir = "${rootProject.buildDir}/snappydata-poc"
  def snappyData = System.env.SNAPPYDATA
  if (snappyData == null || snappyData.length() == 0) {
    snappyData = "${projectDir}/../snappydata"
  }

  doFirst {
    delete productDir
    file("${productDir}/lib").mkdirs()
  }

  // first execute the snappydata "product" target based on env var SNAPPYDATA
  workingDir snappyData
  commandLine './gradlew', 'copyProduct', "-PcopyToDir=${productDir}"

  // lastly copy own assembly fat jar in product lib dir
  doLast {
    copy {
      from assemblyJar
      into "${productDir}/lib"
    }
  }
}
